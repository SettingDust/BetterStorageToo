// For those who want the bleeding edge
buildscript
{
    repositories
    {
        jcenter()
        maven
        {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies
    {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "${minecraft_version}-${mod_version}"
group = "io.github.thestoneman.betterstorage"
archivesBaseName = "BetterStorage"

// java version
sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "${minecraft_version}-${forge_version}"

		if( file('../run').exists() )
	{
		runDir = "../run/assets"
	}
	else
	{
		runDir = "assets"
	}

    mappings = forge_mappings
}

repositories {
    maven {
        name "RX14"
        url "http://mvn.rx14.co.uk/repo/"
    }
    ivy {
        name "Thaumcraft"
        artifactPattern "https://dl.dropboxusercontent.com/u/47135879/[module]-([classifier]-)[revision].[ext]"
    }
    ivy {
        name "MineTweaker3"
        artifactPattern "http://minetweaker3.powerofbytes.com/download/[module]-([classifier]-)[revision].[ext]"
    }
}

dependencies {
    compile name: "InventoryTweaks"   , group: "inventorytweaks", version: invtweaks_version, classifier: "deobf"
    compile name: "Armourers-Workshop", group: "riskyken.armourersWorkshop", version: aw_version, classifier: "api"
    compile name: "NotEnoughItems"    , group: "codechicken", version: nei_version, classifier: "dev"
    runtime name: "CodeChickenLib"    , group: "codechicken", version: ccl_version, classifier: "dev"
    runtime name: "CodeChickenCore"   , group: "codechicken", version: ccc_version, classifier: "dev"
    compile name: "Thaumcraft"        , version: tc_version, classifier: "deobf"
    compile name: "Botania"           , group: "vazkii.botania", version: botania_version, classifier: "deobf"
    runtime name: "Baubles"           , group: "com.azanor.baubles", version: baubles_version, classifier: "deobf"
    compile name: "MineTweaker3"      , group: "minetweaker", version: minetweaker_version, classifier: "Dev"
}

sourceSets
{
    main
    {
        java
        {
        	srcDir 'src/main/java'
        }
        resources
        {
        	srcDir 'src/main/resources'
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs)
    {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs)
    {
        exclude 'mcmod.info'
    }
}

task apiJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.output
    include 'net/mcft/copy/betterstorage/api/**'
    classifier = 'api'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives sourcesJar
    archives apiJar
    archives deobfJar
}
